import "system"
import "math"

class Set {
	fn __init() {
		@_arr = []
	}

	fn add(val) {
		if @has(val): return false

		@_arr.push(val)

		return true
	}

	fn has(val) {
		for var i = 0; i < @_arr.len(); i = i + 1 {
			if @_arr[i] == val: return true
		}

		return false
	}
}

class Sudoku {
	fn __init() {
		@_arr = []

		for var i = 0; i < 81; i = i + 1 {
			@_arr.push(0)
		}
	}

	fn solve() {
		return @_solve(0, 0)
	}

	fn _solve(row, col) {
		if col == 9: return @_solve(row + 1, 0)
		if row == 9: return true
		if @get(row, col) != 0: return @_solve(row, col + 1)

		for var i = 1; i <= 9; i = i + 1 {
			@set(row, col, i)

			if @_check(row, col) and @_solve(row, col + 1): return true
		}

		@set(row, col, 0)

		return false
	}

	fn _emptyIndex() {
		for var i = 0; i < 81; i = i + 1 {
			if !@_arr[i]: return i
		}

		return -1
	}

	fn get(row, col): @_arr[row * 9 + col]

	fn set(row, col, val): @_arr[row * 9 + col] = val

	fn _check(row, col) {
		return @_checkRow(row) and @_checkCol(col) and @_checkSquare(row, col)
	}

	fn _checkRow(row) {
		var set = Set()

		for var i = 0; i < 9; i = i + 1 {
			var val = @get(row, i)

			if val != 0 and !set.add(val): return false
		}

		return true
	}

	fn _checkCol(col) {
		var set = Set()

		for var i = 0; i < 9; i = i + 1 {
			var val = @get(i, col)

			if val != 0 and !set.add(val): return false
		}

		return true
	}

	fn _checkSquare(row, col) {
		var set = Set()

		var rowStart = Math.floor(row / 3) * 3
		var colStart = Math.floor(col / 3) * 3

		for var i = rowStart; i < rowStart + 3; i = i + 1 {
			for var j = colStart; j < colStart + 3; j = j + 1 {
				var val = @get(i, j)

				if val != 0 and !set.add(val): return false
			}
		}

		return true
	}

	fn print() {
		for var i = 0; i < 9; i = i + 1 {
			if i % 3 == 0 {
				for var j = 0; j < 22; j = j + 1: System.print("-")
				System.println("")
			}

			for var j = 0; j < 9; j = j + 1 {
				if j % 3 == 0: System.print("|")

				var cell = @_arr[i * 9 + j]
				if cell {
					System.print(" ")
					System.print(cell)
				} else {
					System.print("  ")
				}
			}

			System.println("|")
		}

		for var j = 0; j < 22; j = j + 1: System.print("-")
		System.println("")
	}
}

var s = Sudoku()

var data = [
	0, 1, 0,   0, 0, 0,   0, 9, 6,
	6, 5, 0,   0, 0, 2,   0, 0, 0,
	0, 0, 4,   0, 5, 0,   2, 0, 0,

	8, 0, 0,   1, 0, 0,   0, 0, 5,
	0, 6, 0,   0, 0, 0,   0, 3, 0,
	2, 0, 0,   0, 0, 4,   0, 0, 1,

	0, 0, 8,   0, 3, 0,   4, 0, 0,
	0, 0, 0,   5, 0, 0,   0, 2, 9,
	5, 9, 0,   0, 0, 0,   0, 6, 0
]

for var i = 0; i < 9; i = i + 1 {
	for var j = 0; j < 9; j = j + 1 {
		s.set(i, j, data[i * 9 + j])
	}
}

System.println("Unsolved:")
s.print()
System.println("")

assert true == s.solve()

System.println("Solved:")
s.print()
System.println("")
